<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet 
	version="1.0"
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:disp="http://www.sparna.fr/thesaurus-display"
	xmlns:fo="http://www.w3.org/1999/XSL/Format">
	
	<xsl:param name="column-count">1</xsl:param>
	
	<xsl:template match="/">
		<xsl:apply-templates select="disp:display" />
	</xsl:template>
	
	<xsl:template match="disp:display">
		<fo:root xmlns:fo="http://www.w3.org/1999/XSL/Format">
		
			<fo:layout-master-set>
				<fo:simple-page-master
			  		master-name="A4"
			  		page-width="210mm"
					page-height="297mm"
					margin-top="2cm"
					margin-bottom="1cm"
					margin-left="2cm"
					margin-right="2cm">
					<fo:region-body column-count="{$column-count}" column-gap="5pt" margin-bottom="1cm" />
					<fo:region-before extent="1cm" />
					<fo:region-after extent="1cm" />
					<fo:region-start extent="1cm" />
					<fo:region-end extent="1cm" />
				</fo:simple-page-master> 
			</fo:layout-master-set>
		
			<fo:page-sequence master-reference="A4">
			
				<!-- adds page number but this does not work, it overlaps with the text -->
				 <!--
				<fo:static-content flow-name="xsl-region-after">
			      <fo:block font-size="10pt" 
			                font-family="sans-serif" 
			                text-align="end"><fo:page-number/></fo:block>
				</fo:static-content>
				-->
				
				<fo:static-content flow-name="xsl-region-after">
					 <xsl:call-template name="footer"/>
				</fo:static-content>
			
  				<fo:flow flow-name="xsl-region-body">
  					<xsl:apply-templates />
  				</fo:flow>
  				
			</fo:page-sequence>
		
		</fo:root>
	</xsl:template>
	
	<xsl:template name="footer">
		<fo:block font-size="7pt" text-align="outside">
			<fo:page-number/> - <fo:inline font-style="italic">Generated by SKOS Play!, sparna.fr</fo:inline>
		</fo:block>
	</xsl:template>
	
	<!-- Display header -->
	<xsl:template match="disp:header">
		<fo:block 
  			padding-before="1em" 
  			padding-after="1em"
  			padding-start="1em"
  			margin-bottom="1em"
  			font-family="sans-serif"
  			span="all"
  			border-before-style="outset"
  			border-after-style="outset"
  			border-start-style="outset"
  			border-end-style="outset"
  		>
  			<fo:block font-size="16pt"><xsl:value-of select="disp:title" /></fo:block>
  			<xsl:apply-templates select="disp:creator" />
  			<xsl:apply-templates select="disp:date" />
			<xsl:apply-templates select="disp:version" />
			<xsl:apply-templates select="disp:description" />
  		</fo:block>
	</xsl:template>
	<xsl:template match="disp:creator | disp:date | disp:version | disp:description">
		<fo:block margin-left="10pt" font-size="10pt"><xsl:value-of select="." /></fo:block>
	</xsl:template>


	<!-- Display body -->
	<xsl:template match="disp:body">
		<!-- if more than one section, and at least have a title -->
		<xsl:if test="count(disp:section[@title]) > 1">
			<!-- nothing special in PDF. Maybe an automatic summary ? -->
		</xsl:if>
		<xsl:apply-templates select="disp:section" />
	</xsl:template>
	
	<!-- Process a section -->
	<xsl:template match="disp:section">
		<fo:block page-break-after="always">
			<xsl:if test="@title">
				<fo:block
					margin-top="9pt"
					margin-bottom="9pt"
					font-size="120%"
					text-align="center"
					font-weight="bold">
					<xsl:value-of select="@title" />
				</fo:block>
			</xsl:if>
			<xsl:apply-templates />
		</fo:block>
	</xsl:template>

	<!-- Display a list -->
	<xsl:template match="disp:list">
		<fo:block font-size="90%">
			<xsl:for-each select="disp:listItem">
				<fo:block margin-top="9pt">
					<xsl:apply-templates select="disp:conceptBlock" />
				</fo:block>
			</xsl:for-each>
		</fo:block>
	</xsl:template>
	
	<!-- Display a tree -->
	<xsl:template match="disp:tree">
		<fo:block font-size="80%">
			<xsl:apply-templates select="disp:node" />
		</fo:block>
	</xsl:template>
	
	<!-- process a tree node -->
	<xsl:template match="disp:node">
		<fo:block margin-left="10pt">
			<!-- print its conceptBlock data -->
			<xsl:apply-templates select="disp:nodeData/disp:conceptBlock" />
			<!-- recurse -->
			<xsl:apply-templates select="disp:node" />
		</fo:block>
	</xsl:template>

	<!-- Display a table -->
	<xsl:template match="disp:table">
		<fo:block font-size="90%">
			<fo:table>
				<fo:table-column column-width="50%"/>
				<fo:table-column column-width="50%"/>
				<xsl:apply-templates select="disp:tableHeader" />
				<fo:table-body>
					<xsl:for-each select="disp:row">
						<fo:table-row>
							<xsl:for-each select="disp:cell">
								<fo:table-cell>
									<fo:block>
										<xsl:apply-templates />
									</fo:block>
								</fo:table-cell>
							</xsl:for-each>
						</fo:table-row>
					</xsl:for-each>
				</fo:table-body>
			</fo:table>
		</fo:block>
	</xsl:template>
	
	<xsl:template match="disp:tableHeader">
		<fo:table-header>
			<fo:table-row>
				<xsl:for-each select="disp:cell">
				<fo:table-cell>
					<fo:block text-align="center" font-weight="bold">
						<xsl:apply-templates />
					</fo:block>
				</fo:table-cell>
				</xsl:for-each>
			</fo:table-row>
		</fo:table-header>
	</xsl:template>


	<!-- display a concept block -->
	<xsl:template match="disp:conceptBlock">
		<xsl:apply-templates select="disp:label" />
		
		<xsl:if test="disp:att | disp:ref">
			<fo:block margin-left="10pt">
				<xsl:apply-templates select="disp:att | disp:ref" />
			</fo:block>
		</xsl:if>
	</xsl:template>

	<xsl:template match="disp:str | disp:label | disp:typedString">
		<xsl:choose>
			<xsl:when test="@type = 'pref'">
				<fo:inline font-weight="bold"><xsl:value-of select="text()" /></fo:inline>
			</xsl:when>
			<xsl:when test="@type = 'alt'">
				<fo:inline font-style="italic"><xsl:value-of select="text()" /></fo:inline>
			</xsl:when>
			<xsl:otherwise>
				<fo:inline><xsl:value-of select="text()" /></fo:inline>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>
	
	<xsl:template match="disp:att">
		<fo:block font-size="smaller"><xsl:value-of select="@type" /> : <xsl:apply-templates /></fo:block>
	</xsl:template>
	
	<xsl:template match="disp:ref">
		<fo:block font-size="smaller"><xsl:value-of select="@type" /> : <xsl:apply-templates /></fo:block>
	</xsl:template>
	
</xsl:stylesheet>